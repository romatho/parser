package parserClasses;
import java_cup.runtime.*;



parser code
{:
  public String filename;

  public parser(java_cup.runtime.Scanner s, String pFilename) {
      super(s);
      filename = pFilename;
  }


  public void errorMessage(int pLine, int pColumn, String pMessage)
  {
    String errorToDisp = this.filename + ":" + pLine + ":" + pColumn + ": syntax error: " + pMessage ;
     System.err.println(errorToDisp);
  }

:};



terminal BOOL, CLASS, DO, ELSE, EXTENDS, FALSE, IF, IN, INT32, ISNULL, LET, NEW, THEN, TRUE, UNIT, WHILE, ASSIGN, DOT;

terminal String STRING, STRINGLITERAL, OBJECTIDENTIFIER, INTEGERLITERAL, TYPEIDENTIFIER;

terminal LBRACE, RBRACE, LPAR, RPAR, COLON, SEMICOLON, COMMA;

terminal AND, EQUAL, LOWER, LOWEREQUAL, DIV, NOT, MINUS, PLUS, POW, TIMES;



non terminal Blocks block;
non terminal Classe class_; /*done*/
non terminal Expressions expression;/*done*/
non terminal Field field; /*done*/
non terminal Formals formals; /*done*/
non terminal Method method; /*done*/
non terminal Program program; /*done*/
non terminal Types types; /*done*/
non terminal ClassBody classBody;


/*Expression*/
/*non terminal Assign assign;
non terminal BinOp binop;
non terminal Call call;
non terminal If if;
non terminal Let let;
non terminal New new;
non terminal Unop unop;
non terminal While while;*/
non terminal Terminal terminaison, boolean;

non terminal ParserArray<Classe> classArray; /*done*/
non terminal ParserArray<Formals> formalArray;/*done*/
non terminal ParserArray<Expressions> expArray, argArray, argOthers; /*done*/


precedence left PLUS, MINUS;

precedence left AND, TIMES, DIV, DOT;

precedence right ASSIGN, ISNULL, POW, OBJECTIDENTIFIER;

precedence left EQUAL;

precedence nonassoc LOWER, LOWEREQUAL, IF;

precedence nonassoc ELSE;

start with program;

program ::=     classArray:ca
                {: RESULT = new Program(0,0, ca); :};

classArray::=   classArray:ca class_:c {: ca.add(c); RESULT = ca; :}
                |                     {: RESULT = new ParserArray<Classe>(); :};


expArray::=     expArray:ea expression:exp1 SEMICOLON {: ea.add(exp1); RESULT = ea; :}
                |                     {: RESULT = new ParserArray<Expressions>(); :};


class_::=        CLASS TYPEIDENTIFIER:name LBRACE classBody:bd  RBRACE
                {: RESULT = new Classe(0, 0, name, bd );:}
                |CLASS TYPEIDENTIFIER:name EXTENDS TYPEIDENTIFIER:parent LBRACE classBody:bd  RBRACE
                {: RESULT = new Classe(0, 0, name, parent, bd);:};

classBody::=    classBody:cb   field:f
                {: cb.addField(f); RESULT=cb; :}
                |classBody:cb method:m
                {: cb.addMethod(m); RESULT=cb;:}
                |
                {:RESULT = new ClassBody(0,0); :};

formalArray::=  formalArray:fa formals:f  COMMA{:  fa.add(f); RESULT =fa; :}
                |                     {: RESULT=new ParserArray<Formals>(); :};


                /*If*/
expression::=   IF expression:exp1 THEN expression:exp2 ELSE expression:exp3
                {:RESULT = new If(0, 0, exp1, exp2, exp3); :}
                |IF expression:exp1 THEN expression:exp2
                {:RESULT = new If(0, 0, exp1, exp2); :}
                |IF error:e
                {: parser.errorMessage(eleft, eright, "Wrong syntax for if expression"); :}

                /*exp = (exp)*/
                | LPAR expression:exp1 RPAR
                {:RESULT = exp1; :}

                /*While*/
                | WHILE expression:exp1 DO expression:exp2
                {:RESULT = new While(0, 0, exp1, exp2); :}
                |WHILE error:e
                {: parser.errorMessage(eleft, eright, "Wrong syntax for while expression"); :}

                /*Let*/
                |LET OBJECTIDENTIFIER:oi COLON types:ti ASSIGN expression:exp1 IN expression:exp2
                {:RESULT = new Let(0, 0, oi, ti, exp1, exp2); :}
                |LET OBJECTIDENTIFIER:oi COLON types:ti IN expression:exp1
                {:RESULT = new Let(0, 0, oi, ti, exp1); :}
                |LET error:e
                {: parser.errorMessage(eleft, eright, "Wrong syntax for let expression"); :}

                /*Assign*/
                |OBJECTIDENTIFIER:oi ASSIGN expression:exp1
                {:RESULT = new Assign(0, 0, oi, exp1); :}

                /*UnOp*/
                |NOT expression:exp1
                {:RESULT = new UnOp(0, 0, "not", exp1); :}
                |MINUS expression:exp1
                {:RESULT = new UnOp(0, 0, "-", exp1); :}
                |ISNULL expression:exp1
                {:RESULT = new UnOp(0, 0, "isnull", exp1); :}

                /*BinOp*/
                |expression:exp1 EQUAL expression:exp2
                {:RESULT = new BinOp(0, 0, "=", exp1, exp2); :}
                |expression:exp1 LOWER expression:exp2
                {:RESULT = new BinOp(0, 0, "<", exp1, exp2); :}
                |expression:exp1 LOWEREQUAL expression:exp2
                {:RESULT = new BinOp(0, 0, "<=", exp1, exp2); :}
                |expression:exp1 PLUS expression:exp2
                {:RESULT = new BinOp(0, 0, "+", exp1, exp2); :}
                |expression:exp1 MINUS expression:exp2
                {:RESULT = new BinOp(0, 0, "-", exp1, exp2); :}
                |expression:exp1 TIMES expression:exp2
                {:RESULT = new BinOp(0, 0, "*", exp1, exp2); :}
                |expression:exp1 POW expression:exp2
                {:RESULT = new BinOp(0, 0, "^", exp1, exp2); :}
                |expression:exp1 DIV expression:exp2
                {:RESULT = new BinOp(0, 0, "//", exp1, exp2); :}
                |expression:exp1 AND expression:exp2
                {:RESULT = new BinOp(0, 0, "AND", exp1, exp2); :}

                /*Call*/
                |OBJECTIDENTIFIER:oi LPAR argArray:aa RPAR
                {:RESULT = new Call(0, 0, new Terminal(0, 0, "self"), oi, aa); :}
                |expression:exp1 DOT OBJECTIDENTIFIER:oi LPAR argArray:aa RPAR
                {:RESULT = new Call(0, 0, exp1, oi, aa); :}

                /*New*/
                |NEW types:ti
                {:RESULT = new New(0, 0, ti);:}
                |NEW error:e
                {: parser.errorMessage(eleft, eright, "Wrong syntax for new expression"); :}

                |terminaison:ter
                {:RESULT = ter; :};

                /*Terminaison*/

terminaison::=  INTEGERLITERAL:it
                {: RESULT = new Terminal(0, 0, it); :}
                |STRINGLITERAL:sl
                {: RESULT = new Terminal(0, 0, sl); :}
                |OBJECTIDENTIFIER:oi
                {: RESULT = new Terminal(0, 0, oi); :}
                |boolean:b
                {: RESULT = b; :};

boolean::=      TRUE
                {: RESULT = new Terminal(0, 0, "true"); :}
                |FALSE
                {: RESULT = new Terminal(0, 0, "false"); :};


field::=        OBJECTIDENTIFIER:oi COLON types:t SEMICOLON
                {:  RESULT = new Field(0,0,oi,t,null); :}
                |OBJECTIDENTIFIER:oi COLON types:t ASSIGN expression:e SEMICOLON
                {: RESULT = new Field(0,0,oi,t,e); :}
                |error:e SEMICOLON
                {: parser.errorMessage(eleft, eright, "Wrong syntax for Field"); :};

formals::=      OBJECTIDENTIFIER:oi COLON types:t
                {: RESULT = new Formals(0,0,oi,t); :};


types::=        TYPEIDENTIFIER:t
                {: RESULT = new Types(0,0,t); :}
                | INT32
                {:  RESULT = new Types(0,0,"int32"); :}
                | BOOL
                {:  RESULT = new Types(0,0,"bool"); :}
                | STRING
                {: RESULT = new Types(0,0,"string"); :}
                | UNIT
                {:  RESULT = new Types(0,0,"unit"); :};



method::=       OBJECTIDENTIFIER:oi LPAR formalArray:fa formals:f RPAR COLON types:t block:b
                {:fa.add(f); RESULT = new Method(0, 0, oi, fa, t, b); :}
                |OBJECTIDENTIFIER:oi LPAR  RPAR COLON types:t block:b
                {: RESULT = new Method(0, 0, oi, new ParserArray<Formals>(), t, b); :}
                |error:e block
                {: parser.errorMessage(eleft, eright, "Wrong syntax for Method"); :};


block::=        LBRACE expArray:ea expression:exp1 RBRACE
                {: ea.add(exp1); RESULT = new Blocks(0,0,ea); :}
                |error:e
                {: parser.errorMessage(eleft, eright, "Wrong syntax for Method"); :};

argArray::=     argOthers:args expression:exp1
                {:args.add(exp1); RESULT=args;:}
                |
                {:RESULT=new ParserArray<Expressions>(); :};

argOthers::=    argOthers:args expression:exp1 COMMA
                {:args.add(exp1); RESULT=args; :}
                |
                {:RESULT=new ParserArray<Expressions>(); :};